plugins {
  id "java-library"
  id "maven-publish"
  id "signing"

  id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
  id "com.google.protobuf" version "0.9.5"
}

repositories {
  // The Google mirror is less flaky than mavenCentral()
  maven { url "https://maven-central.storage-download.googleapis.com/maven2/" }
  mavenCentral()
  mavenLocal()
}

group = "com.authzed.api"
version = findProperty("release") ?: "0.0.0-SNAPSHOT"

nexusPublishing {
  repositories {
    sonatype {
      // Docs here:
      // https://github.com/gradle-nexus/publish-plugin?tab=readme-ov-file#publishing-to-maven-central-via-sonatype-central
      nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
      snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
}}}

publishing {
  publications { authzed(MavenPublication) {
    from components.java
    pom {
      name = "authzed"
      description = "Authzed client library for Java"
      url = "https://github.com/authzed/authzed-java"
      licenses { license {
          name = "The Apache License, Version 2.0"
          url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
      }}
      developers { developer {
        id = "jzelinskie"
        name = "Jimmy Zelinskie"
        email = "jimmy@authzed.com"
      }}
      scm {
        connection = "scm:git:git://github.com/authzed/authzed-java.git"
        developerConnection = "scm:git:ssh://github.com:authzed/authzed-java.git"
        url = "https://github.com/authzed/authzed-java/tree/master"
      }
    }
  }}

  repositories { maven {
    def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
    def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
    def ossrhUsername = findProperty("sonatypeUsername")
    def ossrhPassword = findProperty("sonatypePassword")

    name = "authzed"
    url = project.hasProperty("release") ? releasesRepoUrl : snapshotsRepoUrl
  }}
}

signing {
  def signingKey = findProperty("signingKey")
  def signingPassword = findProperty("signingPassword")
  useInMemoryPgpKeys(signingKey, signingPassword)

  sign publishing.publications.authzed
}


java {
  withJavadocJar()
  withSourcesJar()
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8
}

tasks.sourcesJar {
  // This is necessary to keep gradle from barking at you
  // about an implicit dependency between these two tasks.
  dependsOn tasks.compileJava
}

// All it does is complain about generated code.
javadoc { options.addStringOption('Xdoclint:none', '-quiet') }

def grpcVersion = "1.77.0"
def protocVersion = "4.33.1"
def authzedProtoCommit = "v1.45.4"
def bufDir = "${buildDir}/buf"
def protocPlatformTag = project.findProperty('protoc_platform') ? ":${protoc_platform}" : ""

sourceSets { main {
  proto { srcDir bufDir }
  java  { srcDir "$buildDir/generated" }
  java  { srcDir "$buildDir/src" }
}}

dependencies {
  implementation("io.grpc:grpc-protobuf:${grpcVersion}") {
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'com.google.api.grpc', module: 'proto-google-common-protos'
  }
  implementation "io.grpc:grpc-stub:${grpcVersion}"
  runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
  compileOnly "org.apache.tomcat:annotations-api:6.0.53"

  implementation("build.buf:protovalidate:1.0.1") {
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
    exclude group: 'com.google.code.gson', module: 'gson'
    exclude group: 'com.google.re2j', module: 're2j'
    exclude group: 'com.google.code.findbugs', module: 'annotations'
    exclude group: 'com.google.code.findbugs', module: 'jsr305'
  }
  // In the future this can probably be removed in favor of "protovalidate"
  // See https://buf.build/blog/protoc-gen-validate-v1-and-v2
  implementation("build.buf.protoc-gen-validate:pgv-java-stub:1.2.1") {
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'com.google.protobuf', module: 'protobuf-java-util'
    exclude group: 'com.google.code.gson', module: 'gson'
    exclude group: 'com.google.re2j', module: 're2j'
  }

  api "com.google.protobuf:protobuf-java:${protocVersion}"
  implementation("com.google.protobuf:protobuf-java-util:${protocVersion}") {
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
    exclude group: 'com.google.code.gson', module: 'gson'
  }
  implementation("com.google.api.grpc:proto-google-common-protos:2.63.1") {
    exclude group: 'com.google.protobuf', module: 'protobuf-java'
  }
  implementation "com.google.code.gson:gson:2.13.2"
  implementation "com.google.re2j:re2j:1.8"
  implementation "com.google.code.findbugs:jsr305:3.0.2"
}

// There is no pre-packaged JAR available that contains the gRPC Gateway proto files
task gatewayProtos(type: Exec) {
    mkdir bufDir
    commandLine("buf", "export", "--exclude-imports", "buf.build/grpc-ecosystem/grpc-gateway", "-o", bufDir)
}

task authzedProtos(type: Exec) {
  dependsOn gatewayProtos
  commandLine("buf", "export", "--exclude-imports", "buf.build/authzed/api:${authzedProtoCommit}", "-o", bufDir)
}

protobuf {
  protoc { artifact = "com.google.protobuf:protoc:${protocVersion}${protocPlatformTag}" }
  plugins { grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}${protocPlatformTag}" } }

  generateProtoTasks {
    ofSourceSet("main").each { task -> task.dependsOn authzedProtos }
    all()*.plugins { grpc {} }
  }
}

tasks.named("jar") { manifest {
  attributes("Implementation-Title": project.name,
             "Implementation-Version": project.version)
}}

sourceSets {
  intTest {
    compileClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.main.output
  }
}

configurations {
    intTestImplementation.extendsFrom implementation
    intTestRuntimeOnly.extendsFrom runtimeOnly
}

// Test things
dependencies {
  intTestImplementation "junit:junit:4.13.2"
    intTestImplementation "org.assertj:assertj-core:3.27.6"
}

tasks.register('integrationTest', Test) {
    useJUnit()

    description = 'Runs integration tests.'
    group = 'verification'

    testClassesDirs = sourceSets.intTest.output.classesDirs
    classpath = sourceSets.intTest.runtimeClasspath
    shouldRunAfter test
}
